
export SphericalHarmonics

using LinearAlgebra: norm, dot 

"""
`struct SphericalHarmonics` : datatype representing a real spherical harmonics basis. 

### Constructor to generate the basis object
```julia
basis = SphericalHarmonics(L::Integer; kwargs...)
```

### Keyword arguments:
* `normalisation = :L2` : choose the normalisation of the basis, default is to 
   make it orthonoormal on the unit sphere. 
* `static = (L<=15)` : decide whether to use a generated code that outputs an 
`SVector` but has a larger compiler and stack footprint
* `T = Float64` : datatype in which basis parameters are stored. The output type 
is inferred at runtime, but the rule of thumb is to use `T = FloatX` for 
`FloatX` output.

### Usage example: 
```julia
using StaticArrays, SpheriCart
basis = SphericalHarmonics(4)
# evaluate basis with single input 
ğ« = @SVector randn(3)
Z = basis(ğ«)
Z = compute(basis, ğ«)
# evaluate basis with multiple inputs (batching)
R = [ @SVector randn(3) for _ = 1:32 ]
Z = basis(Rs)
Z = compute(basis, Rs)

# to be implented: 
# Z, âˆ‡Z = compute_and_gradients(basis, ğ«)
# Z, âˆ‡Z, âˆ‡Â²Z = compute_and_hessian(basis, ğ«)
```
See documentation for more details.
"""
struct SphericalHarmonics{L, NORM, STATIC, TF}
   solids::SolidHarmonics{L, NORM, STATIC, TF}
end

SphericalHarmonics(L::Integer; kwargs...) = 
      SphericalHarmonics(SolidHarmonics(L; kwargs...))

Base.getproperty(basis::SphericalHarmonics, prop::Symbol) = (
      prop == :solids ? getfield(basis, :solids) 
                      : getfield(getfield(basis, :solids), prop) )

@inline (basis::SphericalHarmonics)(args...) = compute(basis, args...)

@inline function compute(basis::SphericalHarmonics, ğ«::SVector{3})
   ğ«Ì‚ = ğ« / norm(ğ«)                        
   return compute(basis.solids, ğ«Ì‚)
end 

@inline function compute_with_gradients(basis::SphericalHarmonics, ğ«::SVector{3})
   r = norm(ğ«)
   ğ«Ì‚ = ğ« / r
   Y, âˆ‡Z = compute_with_gradients(basis.solids, ğ«Ì‚)

   âˆ‡Y = map(dZ -> (dz = dZ / r; dz - dot(ğ«Ì‚, dz) * ğ«Ì‚), âˆ‡Z)

   # @inbounds @simd ivdep for i = 1:length(Y)
   #    dz = âˆ‡Y[i] / r
   #    âˆ‡Y[i] = dz - dot(ğ«Ì‚, dz) * ğ«Ì‚
   # end

   return Y, âˆ‡Y
end 

# --------------------- 
#  batched api 


function compute(basis::SphericalHarmonics{L}, 
                  Rs::AbstractVector{<: SVector{3, T1}}
                  ) where {L, T1}  
   Y = similar(Rs, T1, (length(Rs), sizeY(L)))
   compute!(Y, basis, Rs)
   return Y
end

function compute_with_gradients(basis::SphericalHarmonics{L}, 
                  Rs::AbstractVector{<: SVector{3, T1}}
                  ) where {L, T1} 
   Y = similar(Rs, T1, (length(Rs), sizeY(L)))
   âˆ‡Y = similar(Rs, SVector{3, T1}, (length(Rs), sizeY(L)))                  
   compute_with_gradients!(Y, âˆ‡Y, basis, Rs)
   return Y, âˆ‡Y
end 



function _normalise_Rs!(rs, Rs_norm, 
                        basis::SphericalHarmonics, 
                        Rs::AbstractVector{SVector{3, T1}}) where {T1}
   nX = length(Rs) 
   @assert length(rs) == length(Rs_norm) == nX
   @inbounds @simd ivdep for i = 1:nX
      rs[i] = norm(Rs[i])
      Rs_norm[i] = Rs[i] / rs[i]
   end
   return nothing 
end

function _rescale_âˆ‡Z2âˆ‡Y!(âˆ‡Z::AbstractMatrix, Rs_norm, rs)
   nX = length(rs)
   @inbounds for i = 1:size(âˆ‡Z, 2)
      @simd ivdep for j = 1:nX
         dzj = âˆ‡Z[j, i] / rs[j]
         ğ«Ì‚j = Rs_norm[j]
         âˆ‡Z[j, i] = dzj - dot(ğ«Ì‚j, dzj) * ğ«Ì‚j
      end
   end
end




function compute!(Y, basis::SphericalHarmonics, 
                  Rs::AbstractVector{<: SVector{3, T1}}
                  ) where {T1}
   @no_escape begin       
      nX = length(Rs)            
      rs = @alloc(T1, nX)
      Rs_norm = @alloc(SVector{3, T1}, nX)
      _normalise_Rs!(rs, Rs_norm, basis, Rs)
      compute!(Y, basis.solids, Rs_norm)
      nothing 
   end
   return Y
end



function compute_with_gradients!(Y, âˆ‡Y, basis::SphericalHarmonics, 
                        Rs::AbstractVector{<: SVector{3, T1}}
                        ) where {T1}
   @no_escape begin      
      nX = length(Rs)             
      rs = @alloc(T1, nX)
      Rs_norm = @alloc(SVector{3, T1}, nX)
      _normalise_Rs!(rs, Rs_norm, basis, Rs)
      compute_with_gradients!(Y, âˆ‡Y, basis.solids, Rs_norm)
      _rescale_âˆ‡Z2âˆ‡Y!(âˆ‡Y, Rs_norm, rs)
      nothing 
   end 
   return Y, âˆ‡Y
end 


# ------------------------------------------
#  KernelAbstractions in-place api 

using KernelAbstractions
using GPUArraysCore: AbstractGPUVector, AbstractGPUMatrix


function compute!(Y::AbstractGPUMatrix, 
                  basis::SphericalHarmonics{L}, 
                  Rs::AbstractGPUVector{<: SVector{3, T1}}
                  ) where {L, T1}
   # note the Val{true}() means that inputs Rs will be rescaled to unit 
   # length, and the gradient corrected accordingly
   ka_solid_harmonics!(Y, nothing, Val{L}(), Val{true}(), 
                       Rs, basis.Flm)
   return Y
end


function compute_with_gradients!(Y, âˆ‡Y, 
                  basis::SphericalHarmonics{L}, 
                  Rs::AbstractGPUVector{<: SVector{3, T1}}
                  ) where {L, T1} 
   ka_solid_harmonics!(Y, âˆ‡Y, Val{L}(), Val{true}(), 
                       Rs, basis.Flm)
   return Y, âˆ‡Y
end 
